/*
 * Programing Language C-- "Compiler"
 *    Tokuyama kousen Educational Computer 16bit Ver.
 *
 * Copyright (C) 2002-2022 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 *
 */

/*
 * namtbl.cmm : 名前表(Name Table)の管理プログラム
 *
 * 2022.11.22         : C-- に変更，salloc を使用するように変更
 * 2021.03.20         : 名前の衝突検出がScPRM, ScFLDに対応
 * 2016.09.24         : デバッグ用の ntPrint() 追加
 * 2015.08.31 v2.1.0  : CLang の警告が出ないように変更
 * 2012.12.30         : error2 関数を使用するように変更
 * 2010.07.20         : Subversion による管理を開始
 * 2010.03.30 v0.9.11 : ntOffs を ntCnt に変更
 * 2010.03.12 v0.9.10 : public 修飾に対応
 * 2009.07.22 v0.8.0  : 初期バージョン(syntax.c から分離)
 *
 * $Id$
 *
 */

#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#include "util.hmm"
#include "namtbl.hmm"

/* 名前表 */
#define NtMAX      300                 // 表のサイズ
int      ntNextIdx;                    // 次に登録する位置、スコープにより逆戻り
char[][] ntName = array(NtMAX);        // 名前のつづり
int[]    ntScope = array(NtMAX);       // スコープ(ScXXXX を格納する)
int[]    ntType = array(NtMAX);        // 型(TyVOID,TyINT,TyCHAR, ..., 構造体型)
int[]    ntDim = array(NtMAX);         // 配列変数の場合、配列の次元
int[]    ntCnt = array(NtMAX);         // 局所変数、仮引数の場合は番号
                                       //   関数、構造体の場合は、
                                       //     引数やフィールドの数
boolean[] ntPub = array(NtMAX);        // 名前が外部公開される

/* 公開関数 */

// 定義済みの関数名、変数名、仮引数名を探す(getFactorから呼ばれる)
public int ntSrcName(char[] str) {
  for (int i=ntNextIdx-1; i>=0; i=i-1)           // 表の最後(深いスコープ)から
    if (ntScope[i]>=ScPROT &&                    // 順に、関数名、大域変数、
	strCmp(ntName[i], str)==0) return i;     // 局所変数、現在の関数の仮引
  error2("未定義の名前", str);                   // 数から同じ名前のものを探す
  return -1;                                     // ここが実行されることはない
}

// 構造体名を探し型を返す(構造体型は非正値, isTypeから呼ばれる)
public int ntSrcStruct(char[] str) {
  for (int i=0; i<ntNextIdx; i=i+1)              // 表全体について
    if (ntScope[i]==ScSTRC &&                    // 構造体のスコープで
	strCmp(ntName[i], str)==0) return -i;    // 名前の同じものをさがす
  return TyNON;                                  // 見付からない場合は TyNON
}

// 構造体(n)からフィールド名を探す(getPostOPから呼ばれる)
public int ntSrcField(int n, char[] str) {       // n は構造体型(非正値)
  for (int i=-n+1; i<=-n+ntCnt[-n]; i=i+1)       // ntCnt[-n]はフィールド数
    if (strCmp(ntName[i], str)==0) return i;     // フィールド名と比較
  error2("未定義の構造体フィールド名", str);
  return -1;                                     // ここが実行されることはない
}

// 大域名(n)と２重定義になる関数名、大域変数名を探す
public int ntSrcGlob(int n) {                    // n は名前表の添字
  for (int i=0; i<n; i=i+1)                      // n より前にあるはず
    if ((ntScope[i]==ScPROT || ntScope[i]==ScFUNC ||
	 ntScope[i]==ScCOMM || ntScope[i]==ScGVAR) &&
	strCmp(ntName[i], ntName[n])==0)
      return i;                                  // 見つかった
  return -1;                                     // 見つからなかった
}

// ２重定義にならないかチェック(ntDefNameから呼ばれ、関数名と大域変数名を除く)
int ntChkName(char[] str, int scope) {
  for (int i=0; i<ntNextIdx; i=i+1) {            // 表全体について
    if (ntScope[i]!=ScVOID && strCmp(ntName[i],str)==0){// 衝突の可能性を調べる
      if (scope==ScSTRC || ntScope[i]==ScSTRC)   // 構造体名は他の全てと衝突
	return i;
      if (scope>=ScLVAR)                         // ScLVAR, ScFLD, ScPRM は
        if (ntScope[i]==scope) return i;         //   同じスコープなら衝突
      if (ntScope[i]==ScPRM && scope==ScLVAR)    // 仮引数と局所変数は衝突
         return i;
    }
  }
  return -1;
}

//if (curScope!=ScPROT&&curType==VOID&&curDim==0)// 関数以外では void型はポイ
//  error("void型変数/引数は使用できない");      // ンタだけ
//curName         = name[nextIdx];               // 最後に登録した名前

// 名前を登録する(getName, getParams から呼ばれる)
public int ntDefName(char[] name, int scope,
                     int type, int dim, int cnt, boolean pub){
  if (ntNextIdx>=NtMAX) error("名前表がパンク"); // 名前表がパンクした
  if (ntChkName(name, scope)>=0) error2("名前の２重定義", name);
  ntName[ntNextIdx]= salloc(strLen(name));       // 文字列領域を割り当て
  strCpy(ntName[ntNextIdx], name);               // 名前をコピーする
  ntScope[ntNextIdx] = scope;                    // スコープを登録する
  ntType[ntNextIdx]  = type;                     // 型を登録する
  ntDim[ntNextIdx]   = dim;                      // 次元を登録する
  ntCnt[ntNextIdx]   = cnt;                      // 値を登録する
  ntPub[ntNextIdx]   = pub;                      // 外部名
  int tmpIdx = ntNextIdx;
  ntNextIdx = tmpIdx + 1;
  return tmpIdx;
}

// 名前表を印刷する（デバッグ用
/*
void ntPrint() {
  for (int i=0; i<ntNextIdx; i=i+1) {
    printf("%3d ",     i);
    printf("%-8s ",    ntName[i] );
    printf("%2d ",     ntScope[i]);
    printf("%2d ",     ntType[i] );
    printf("%d ",      ntDim[i]  );
    printf("%2d ",     ntCnt[i]  );
    printf("%d\n",     ntPub[i]  );
  }
}
*/

// 名前表をもとに戻す
public void ntUndefName(int idx) {               // idx まで戻す
  //ntPrint();                                   // ＃デバッグ用
  for (int i=idx; i<ntNextIdx; i=i+1)            // 名前のつづり用の領域を順に
    free(ntName[i]);                             // 解放していく
  ntNextIdx = idx;                               // nextIdx を戻せば完成
}

// スコープを検索対象外に設定
public void ntSetVoid(int idx) {
  //ntPrint();                                   // ＃デバッグ用
  for (int i=idx; i<ntNextIdx; i=i+1)
    ntScope[i] = ScVOID;
}

// 名前表のサイズを返す
public int ntGetSize() { return ntNextIdx; }

// 名前表からデータを取り出す
public char[] ntGetName(int idx) { return ntName[idx]; }
public int ntGetScope(int idx) { return ntScope[idx]; }
public int ntGetType(int idx) { return ntType[idx]; }
public int ntGetDim(int idx) { return ntDim[idx]; }
public int ntGetCnt(int idx) { return ntCnt[idx]; }
public boolean ntGetPub(int idx) { return ntPub[idx]; }

// 名前表にデータを書き込む
public void ntSetName(int idx, char[] v) { ntName[idx] = v; }
public void ntSetScope(int idx, int v) { ntScope[idx] = v; }
public void ntSetType(int idx, int v) { ntType[idx]=v; }
public void ntSetDim(int idx, int v) { ntDim[idx]=v; }
public void ntSetCnt(int idx, int v) { ntCnt[idx]=v; }
public void ntSetPub(int idx, boolean v) { ntPub[idx]=v; }
